apply plugin: 'com.android.application'

def getGitVersionCode = {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1
    }
}

def getGitVersionName = {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            workingDir projectDir
            executable 'git'
            args 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return ""
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.zeerd.dltviewer"
        ndk {
            moduleName "dlt-jnicallback"
        }
        minSdkVersion 23

        versionCode getGitVersionCode()
        versionName "0.1.2-" + getGitVersionName() + "-r" + getGitVersionCode()
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    externalNativeBuild {
        ndkBuild {
            path file('src/main/jni/Android.mk')
        }
    }
    productFlavors {
    }
}

dependencies {
}
